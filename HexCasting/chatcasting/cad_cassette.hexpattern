#git(/HexCasting/libs/base.hexpattern)

// stack: [params], first in list is spell name which i want to cad

$check_for_free_cassette(Akasha CAD)

$stop_detect(Akasha CAD)

Reveal
Speaker's Decomposition
Jester's Gambit
Huginn's Gambit
@str(to hexicon)

{@vec(375, 53, 2484)} >>
Jester's Gambit
Akasha's Distillation
Gemini Decomposition
Augur's Purification
$fastif()(
    // quit if no pattern for passed name
    Janus' Gambit
)
// stack: [spell]
{
    {
        Nullary Reflection
    }
    Flock's Disintegration
}
Numerical Reflection: 1
Muninn's Reflection
Surgeon's Exaltation
Jester's Gambit
Additive Distillation
// stack: [{[params]}>>, spell]

{
    Telepathic Reflection
    Numerical Reflection: -1
    Inequality Distillation
    {
        $rightclick
        Numerical Reflection: 10
        Minimus Distillation II
        $rightclick
        Numerical Reflection: 1
        Maximus Distillation II
        Conjunction Distillation
        {
            {
                Nullary Reflection
            } >>
            Athena's Gambit
            Bookkeeper's Gambit: v
            Athena's Revelation
        }
        {}
        Augur's Exaltation
        Hermes' Gambit
    }
    {}
    Augur's Exaltation
    Hermes' Gambit

    {@str(Akasha CAD)} >>
    Program Purification
    Numerical Reflection: 10
    {@str(Akasha CAD)} >>
    Enqueue
}
Numerical Reflection: 9
Rotation Gambit
Surgeon's Exaltation

Numerical Reflection: 10
{@str(Akasha CAD)} >>
Enqueue
