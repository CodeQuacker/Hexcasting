// vector, pattern, any, bool -> 

#def(check_akasha)(
    // vector, pattern ->
    Akasha's Distillation
    Nullary Reflection
    Inequality Distillation
    {
        @str(Hex with this pattern already exists)
        Reveal
        Janus' Gambit
    }
    {

    }
    Augur's Exaltation
)

Flock's Reflection
Flock's Gambit

// check if there are 4 params
Gemini Decomposition
Length Purification
Numerical Reflection: 4
Equality Distillation
{
    // check if force is true
    Gemini Decomposition
    Flock's Disintegration
    Bookkeeper's Gambit: vvv-
    True Reflection
    Equality Distillation
    {
        // allow to overwrite the previous spell with this pattern
    }
    {
        // stop hex if pattern exists
        $check_akasha
    }
    Augur's Exaltation
    
}
{
    // if no bool is passed for forcing overwrite, stop hex if pattern exists
    $check_akasha
}
Augur's Exaltation

// cleanup force param
Flock's Disintegration
Bookkeeper's Gambit: v
Length Purification
Flock's Gambit


// add to akasha
Gemini Decomposition
Flock's Disintegration
Akasha's Gambit

// take from akasha to everbook
Gemini Decomposition
Flock's Disintegration
Bookkeeper's Gambit: --v
Elysium's Gambit

// make natural
Flock's Disintegration
Bookkeeper's Gambit: v-v
Brigh's Gambit